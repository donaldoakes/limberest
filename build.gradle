plugins {
	id 'java'
    id 'maven'
    id 'signing'
}

group = 'io.limberest'
version = '1.1.5'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

sourceSets {
	main {
		java {
			srcDir 'src'
		}
		output.classesDir = 'build/classes'
	}
}

jar {
    from('.') {
       include 'codegen/**/*'
    }
}
    
dependencies {
	compile 'javax.servlet:javax.servlet-api:3.1.0'
	compile 'javax.ws.rs:javax.ws.rs-api:2.0'
    compile 'javax.validation:validation-api:1.1.0.Final'
	compile 'org.json:json:20160810'
	compile 'org.slf4j:slf4j-api:1.7.22'
	compile 'com.google.guava:guava:21.0'
    compile 'commons-codec:commons-codec:1.10'
	compile 'org.reflections:reflections:0.9.10'
	compile 'io.swagger:swagger-annotations:1.5.13'
	compile 'io.swagger:swagger-core:1.5.13'
	compile 'io.swagger:swagger-models:1.5.13'
	compile 'io.swagger:swagger-servlet:1.5.13'
	compile 'com.fasterxml.jackson.core:jackson-core:2.8.6'
	compile 'com.fasterxml.jackson.core:jackson-databind:2.8.6'
    compileOnly 'io.swagger:swagger-codegen:2.3.1'
}

task cleanJavadocs(type: Delete) {
    delete 'docs/javadoc'
}

task javadocs(type: Javadoc, dependsOn: cleanJavadocs) {
    classpath = configurations.compile
    source = sourceSets.main.allJava
    title = 'Limberest API'
    maxMemory = '256m'
    options.links 'http://docs.oracle.com/javase/8/docs/api/'
    options.links 'http://docs.swagger.io/swagger-core/current/apidocs/'
    options.links 'http://stleary.github.io/JSON-java/'
    options.bottom '<i>Copyright &#169; 2018</i>'
    destinationDir = file('docs/javadoc')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from file('docs/javadoc')
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
  
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
  
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
  
            pom.project {
                name 'Limberest'
                description 'Lightweight REST for Java'
                packaging 'jar'
                url 'https://limberest.io'
  
                scm {
                    connection 'scm:git:https://github.com/limberest/limberest.git'
                    developerConnection 'scm:git:https://github.com/limberest/limberest.git'
                    url 'https://github.com/limberest/limberest'

                }
  
                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
  
                developers {
                    developer {
                        id 'oakesville'
                        name 'Donald Oakes'
                        email 'donald@oakesville.io'
                    }
                }
            }
        }
    }
}
